#include "common_ps_fxc.h"

sampler BaseTextureSampler	: register( s0 );

struct PS_INPUT
{
	float2 baseTexCoord				: TEXCOORD0;
};

float3 DecompressHDRFromTexture( float4 input )
{
	return input.rgb * input.a * MAX_HDR_OVERBRIGHT;
}

float4 CompressHDRToTexture( float3 input )
{
	// FIXME: want to use min so that we clamp to white, but what happens if we 
	// have an albedo component that's less than 1/MAX_HDR_OVERBRIGHT?
	//	float fMax = max( max( color.r, color.g ), color.b );
	float4 output;
	float fMax = min( min( input.r, input.g ), input.b );
//	float fMax = max( max( input.r, input.g ), input.b );
	if( fMax > 1.0f )
	{
		float oofMax = 1.0f / fMax;
		output.rgb = oofMax * input.rgb;
		output.a = min( fMax / MAX_HDR_OVERBRIGHT, 1.0f );
	}
	else
	{
		output.rgb = input.rgb;
		output.a = 1.0f / MAX_HDR_OVERBRIGHT;
	}
	return output;
}

// This is used when reading from a render target (water, for instance.)
float3 DecompressHDRFromRenderTarget( float4 input )
{
	float lumRGB = Luminance( input.rgb );
	float lumA = input.a * MAX_HDR_OVERBRIGHT;
	// FIXME: tweak this value.
	if( lumA > lumRGB * 1.1f )
	{
		return input.rgb * ( lumA / lumRGB );
	}
	else
	{
		return input.rgb;
	}
}

// This is used for almost all rendering so that alpha blending will work properly.
float4 CompressHDRToRenderTarget( float3 input )
{
	float lum = Luminance( input ) * ( 1.0f / MAX_HDR_OVERBRIGHT );
	// go ahead and let the hardware clamp the value here!!!!
	return float4( input, lum );
}

HALF4 main( PS_INPUT i ) : COLOR
{
	HALF4 color = tex2D( BaseTextureSampler, i.baseTexCoord );
	float3 decompressedColor = color.rgb * (color.a + TONEMAP_SCALE_LINEAR);
	
	float4 compressedColor = CompressHDRToTexture(decompressedColor);
	return compressedColor;
}

